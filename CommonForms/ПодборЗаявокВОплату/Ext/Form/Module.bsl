
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокЗаявка.Дата КАК Дата,
		|	ДокЗаявка.Ссылка КАК Заявка,
		|	// ДокЗаявка.СуммаДокумента - ЕСТЬNULL(ОплатаЗаявокОбороты.СуммаОборот, 0) КАК Остаток
		|	ЕСТЬNull(ВзаиморасчетыОстатки.СуммаОстаток,0) КАК Остаток
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.Заявка КАК ДокЗаявка
		|		// ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОплатаЗаявок.Обороты КАК ОплатаЗаявокОбороты
		|		// ПО (ОплатаЗаявокОбороты.Заявка = ДокЗаявка.Ссылка)
		|    ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Взаиморасчеты.Остатки КАК ВзаиморасчетыОстатки
		|    	ПО ДокЗаявка.Ссылка = ВзаиморасчетыОстатки.Заявка
		|ГДЕ
		|	НЕ ДокЗаявка.ПометкаУдаления
		|	И ДокЗаявка.Договор = &Договор
		|	// И ДокЗаявка.СуммаДокумента - ЕСТЬNULL(ОплатаЗаявокОбороты.СуммаОборот, 0) > 0   
		|	И ЕСТЬNull(ВзаиморасчетыОстатки.СуммаОстаток,0)  > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Дата КАК Дата,
		|	ВТ.Заявка КАК Заявка,
		|	ВТ.Остаток КАК Остаток,
		|	МАКСИМУМ(ДокСчет.Номер) КАК НомерСчета
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК ДокСчет
		|		ПО ВТ.Заявка = ДокСчет.Основание
		|			И (НЕ ДокСчет.ПометкаУдаления)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Дата,
		|	ВТ.Заявка,
		|	ВТ.Остаток
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ.Дата";
	
	Запрос.УстановитьПараметр("Договор", Параметры.Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрТЗ = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(СтрТЗ, Выборка);
	КонецЦикла;
	
	ТаблПодобранных = ПолучитьИзВременногоХранилища(Параметры.АдресПодобранных);
	
	Для Каждого СтрПод Из ТаблПодобранных Цикл
		СтрокиТЗ = ТЗ.НайтиСтроки(Новый Структура("Заявка", СтрПод.Заявка));
		Если СтрокиТЗ.Количество()>0 Тогда
			СтрокиТЗ[0].Сумма = СтрПод.Сумма; 
		КонецЕсли;
	КонецЦикла;
	НераспределеннаяСумма = Параметры.СуммаДокумента - ТЗ.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗСуммаПриИзменении(Элемент)
	НераспределеннаяСумма = Параметры.СуммаДокумента - ТЗ.Итог("Сумма");
КонецПроцедуры

&НаКлиенте
Процедура Автоподбор(Команда)

	Для Каждого СтрТЗ Из ТЗ Цикл
		Если СтрТЗ.Сумма = СтрТЗ.Остаток Тогда
			Продолжить;
		КонецЕсли;
		Доступно = СтрТЗ.Остаток - СтрТЗ.Сумма;
		Закрыть = Мин(Доступно, НераспределеннаяСумма);
		Если Закрыть > 0 Тогда
			СтрТЗ.Сумма = СтрТЗ.Сумма + Закрыть;
			НераспределеннаяСумма = НераспределеннаяСумма - Закрыть;
		КонецЕсли;
		Если НераспределеннаяСумма = 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицу()
	
	ТаблицаРезультат = Новый ТаблицаЗначений;
	ТаблицаРезультат.Колонки.Добавить("Заявка", Новый ОписаниеТипов("ДокументСсылка.Заявка"));
	ТаблицаРезультат.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(12, 2, ДопустимыйЗнак.Любой)));
	Для Каждого Тек Из ТЗ Цикл
		Если Тек.Сумма > 0 Тогда
			НовТЗ = ТаблицаРезультат.Добавить();
			НовТЗ.Заявка = Тек.Заявка;
			НовТЗ.Сумма = Тек.Сумма;
		КонецЕсли;
	КонецЦикла;
	Если НераспределеннаяСумма > 0 Тогда
		НовТЗ = ТаблицаРезультат.Добавить();
		//НовТЗ.Заявка = Тек.Заявка;
		НовТЗ.Сумма = НераспределеннаяСумма;
	КонецЕсли;
	Возврат ПоместитьВоВременноеХранилище(ТаблицаРезультат);
	
КонецФункции

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(ПолучитьТаблицу());
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры
