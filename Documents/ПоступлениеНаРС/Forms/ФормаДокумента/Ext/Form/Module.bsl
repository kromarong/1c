&НаСервере
Процедура ВидимостьДоступность()
	
	Если Режим = "Многострочный" Тогда
		Элементы.СтраницыРасшифровки.ТекущаяСтраница = Элементы.СтраницаМногострочный;
	Иначе
		Элементы.СтраницыРасшифровки.ТекущаяСтраница = Элементы.СтраницаОднострочный;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРазбитьПлатежНажатие(Элемент)
	
	Режим = "Многострочный";
	ВидимостьДоступность();
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Объект.РасшифровкаПлатежа.Количество()>1 Тогда
		Режим = "Многострочный";
	Иначе
		Режим = "Однострочный";
	КонецЕсли;
	ВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Режим = "Однострочный" Тогда
		ТекущийОбъект.РасшифровкаПлатежа.Очистить();
		НовРП = ТекущийОбъект.РасшифровкаПлатежа.Добавить();
		НовРП.Заявка = ТекущийОбъект.Заявка;
		НовРП.Сумма = ТекущийОбъект.СуммаДокумента;
	Иначе
		ТекущийОбъект.СуммаДокумента = ТекущийОбъект.РасшифровкаПлатежа.Итог("Сумма");
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПоместитьУжеПодобранные()
	
	СтруктураРезультат = Новый Структура;
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Заявка", Новый ОписаниеТипов("ДокументСсылка.Заявка"));
	ТЗ.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(12, 2, ДопустимыйЗнак.Любой)));
	
	КоличествоПодобранных = 0;
	
	Если Режим = "Однострочный" Тогда
		НовТЗ = ТЗ.Добавить();
		НовТЗ.Заявка = Объект.Заявка;
		НовТЗ.Сумма = Объект.СуммаДокумента;
		Если ЗначениеЗаполнено(НовТЗ.Заявка)Тогда
			КоличествоПодобранных = КоличествоПодобранных + 1;
		КонецЕсли;
	Иначе	
		Для Каждого СтрРП Из Объект.РасшифровкаПлатежа Цикл
			Если Не ЗначениеЗаполнено(СтрРП.Заявка) Тогда
				Продолжить;
			КонецЕсли;
			НовТЗ = ТЗ.Добавить();
			НовТЗ.Заявка = СтрРП.Заявка;
			НовТЗ.Сумма = СтрРП.Сумма;
			Если ЗначениеЗаполнено(НовТЗ.Заявка)Тогда
				КоличествоПодобранных = КоличествоПодобранных + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтруктураРезультат.Вставить("КоличествоПодобранных", КоличествоПодобранных);
	СтруктураРезультат.Вставить("АдресПодобранных", ПоместитьВоВременноеХранилище(ТЗ,ЭтаФорма.УникальныйИдентификатор));
	Возврат СтруктураРезультат;
	
КонецФункции

&НаСервере
Процедура ВнестиЗаявкиИзПодбора(РезультатПодбора)
	
	ТаблицаПодобранных = ПолучитьИзВременногоХранилища(РезультатПодбора);
	Объект.РасшифровкаПлатежа.Очистить();
	Если ТаблицаПодобранных.Количество()>1 Тогда
		Объект.Заявка = "";
		Для Каждого СтрТП Из ТаблицаПодобранных Цикл
			СтрРП = Объект.РасшифровкаПлатежа.Добавить();
			СтрРП.Заявка = СтрТП.Заявка;
			СтрРП.Сумма = СтрТП.Сумма;
			Режим = "Многострочный";
		КонецЦикла;
	ИначеЕсли ТаблицаПодобранных.Количество()>0 Тогда
		Объект.Заявка = ТаблицаПодобранных[0].Заявка;
		Объект.СуммаДокумента = ТаблицаПодобранных[0].Сумма;
		Режим = "Однострочный"; 
	КонецЕсли;
	ВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодбораЗаявок(АдресПодобранных)
	
	ПараметрыФ = Новый Структура;
	ПараметрыФ.Вставить("АдресПодобранных", АдресПодобранных);
	ПараметрыФ.Вставить("Договор", Объект.Договор);
	ПараметрыФ.Вставить("СуммаДокумента", Объект.СуммаДокумента);
	РезультатПодбора = ОткрытьФормуМодально("ОбщаяФорма.ПодборЗаявокВОплату", ПараметрыФ);
	Если РезультатПодбора <> Неопределено Тогда
		ВнестиЗаявкиИзПодбора(РезультатПодбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатВопросаОтмены(Результат, АдресПодобранных)Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
		ОткрытьФормуПодбораЗаявок(АдресПодобранных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодобратьЗаявкиНажатие(Элемент)
	
	СтруктураУжеПодобранных = ПоместитьУжеПодобранные();
	АдресПодобранных = СтруктураУжеПодобранных.АдресПодобранных;            
	КоличествоПодобранных = СтруктураУжеПодобранных.КоличествоПодобранных; 
	Оповещение = Новый ОписаниеОповещения("РезультатВопросаОтмены", ЭтотОбъект, АдресПодобранных);
	Если КоличествоПодобранных > 0 И Объект.Проведен Тогда
		ПоказатьВопрос(Оповещение, "Перед заполнением будет отменено проведение документа. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ОткрытьФормуПодбораЗаявок(АдресПодобранных);
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура АвтоподборНаСервере(ОтменаПроведения) 
	
	Если ОтменаПроведения Тогда 
		ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения);
		Записать(ПараметрыЗаписи);	
	КонецЕсли;
	
	Объект.РасшифровкаПлатежа.Очистить();   
	
	НикаСервер.АвтоЗаполнитьРасшифровку(Объект);	 
	
	Если Объект.РасшифровкаПлатежа.Количество()>1 Тогда
		Объект.Заявка = "";
		Режим = "Многострочный";
	ИначеЕсли Объект.РасшифровкаПлатежа.Количество()>0 Тогда
		Объект.Заявка = Объект.РасшифровкаПлатежа[0].Заявка;
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].Сумма;
		Режим = "Однострочный"; 
	КонецЕсли;
	ВидимостьДоступность();    
	
КонецПроцедуры

&НаКлиенте
Процедура Автоподбор(Команда) 
	
	Если Объект.Проведен Тогда
		Если Вопрос("Для подбора будет отменено проведение документа. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		АвтоподборНаСервере(Истина);   
	Иначе
		АвтоподборНаСервере(Ложь);   
	КонецЕсли;
	
КонецПроцедуры
